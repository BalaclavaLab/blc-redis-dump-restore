import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

group 'com.balaclavalab'
version '0.1.3'

description = 'BLC Redis dump restore utility'
mainClassName = 'com.balaclavalab.redis.DumpRestoreCli'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.11'
    }
}

dependencies {
    implementation 'io.lettuce:lettuce-core:5.1.3.RELEASE'
    implementation 'commons-cli:commons-cli:1.4'
}

docker {
    javaApplication {
        baseImage = 'openjdk:11'
        port = 8080
        tag = "balaclavalab/${applicationName}:${version}"
    }
}

// TODO https://github.com/bmuschko/gradle-docker-plugin/issues/41
dockerPushImage.imageName = "balaclavalab/${applicationName}"
dockerPushImage.tag = "${version}"

task dockerTagLatestImage(type: DockerTagImage) {
    repository = "balaclavalab/${applicationName}"
    imageId = "balaclavalab/${applicationName}:${version}"
    tag = "latest"
    force = true
}

task dockerPushLatestImage(type: DockerPushImage) {
    imageName = "balaclavalab/${applicationName}"
    tag = "latest"
}
